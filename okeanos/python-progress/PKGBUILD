# Maintainer: Axilleas Pipinellis <axilleas@archlinux.gr>

pkgname=('python2-progress' 'python-progress')
pkgver=1.0.1
pkgrel=1
pkgdesc="Easy to use progress bars"
url="http://docs.dev.grnet.gr/pypi/"
arch=('any')
license=('ISC')
makedepends=('python2-distribute' 'python-distribute')
source=(http://docs.dev.grnet.gr/pypi/progress-$pkgver.tar.gz)


build() {
  cd "$srcdir"

  rm -rf python{,2}-build
  for builddir in python{,2}-build; do
    cp -r progress-$pkgver $builddir
    pushd $builddir
    ${builddir%-build} setup.py build
    popd
  done
}

package_python-progress() {
  depends=('python-distribute')

  cd "$srcdir/python-build"

  python setup.py install --root="$pkgdir" -O1

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-progress() {
  depends=('python2-distribute')

  cd "$srcdir/python2-build"

  python2 setup.py install --root="$pkgdir" -O1

  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

# vim:set ts=2 sw=2 et:

md5sums=('73ec2b6b5d2ed0ffb53c7f2a7d79a23e')
